// Pixel shader for shadows maps
// with 4 higher than 8-bit precision samples
// and percentage closer filtering
#include "../shade.inl"

ps.1.4
// Intensity in shadow
def c0, SHADE, SHADE, SHADE, 1
// Intensity in the light
def c1, 1, 1, 1, 1
// Sample weights (1/3)
def c3, 0.33, 0.33, 0.33, 0.0

// Fetch 3 shadow map samples
texld r2, t2
texld r3, t3
texld r4, t4
// Light space depth from vertex shader
texcrd r5.rgb, t5

// Unpack sample 0
mov_d8 r2.r, r2.r
add r2.r, r2.r, r2.g
// Unpack sample 1
mov_d8 r3.r, r3.r
add r2.g, r3.r, r3.g
// Unpack sample 2
mov_d8 r4.r, r4.r
add r2.b, r4.r, r4.g
	
// Compare shadow map samples to surface depth
sub r2.rgb, r5.b, r2
cmp r2.rgb, -r2, c1, c0

phase
texld r0, t0
texld r1, t1
// Compute average
dp3 r3, r2, c3
//r3 - shadow coefficient

mad r0.rgb,r0,v0,v1
add r0.rgb,r0,r1
mul r0,r0,r3

